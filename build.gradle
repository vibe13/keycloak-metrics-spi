import java.text.SimpleDateFormat

plugins {
    id "net.nemerosa.versioning" version "2.8.2"
}

repositories {
    jcenter()
}

configurations {
    bundleLib
}

group 'org.jboss.aerogear'
version '1.0.3-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
    keycloakVersion=project.properties["keycloakVersion"]
    prometheusVersion=project.properties["prometheusVersion"]
}


dependencies {
    compile group: 'org.keycloak', name: 'keycloak-server-spi-private', version: keycloakVersion
    compile group: 'org.keycloak', name: 'keycloak-server-spi', version: keycloakVersion
    compile group: 'org.keycloak', name: 'keycloak-services', version: keycloakVersion
    compile group: 'org.jboss.spec.javax.ws.rs', name: 'jboss-jaxrs-api_2.0_spec', version: '1.0.0.Final'
    compile group: 'com.google.guava', name: 'guava', version: '11.0.2'
    bundleLib group: 'io.prometheus', name: 'simpleclient_common', version: prometheusVersion
    bundleLib group: 'io.prometheus', name: 'simpleclient_hotspot', version: prometheusVersion
    configurations.compile.extendsFrom(configurations.bundleLib)

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name 'redhat-ga'
            url 'https://maven.repository.redhat.com/ga/'
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }

        repositories {
            // publishing in PNC
            if (System.getProperty('AProxDeployUrl') != null) {
                maven {
                    name = "PNC"
                    url = System.getProperty('AProxDeployUrl')
                    credentials(HttpHeaderCredentials) {
                        name = "Authorization"
                        value = "Bearer " + System.getProperty('accessToken')
                    }
                    authentication {
                        header(HttpHeaderAuthentication)
                    }
                }
            }
        }
    }
}

jar {
    from {
        configurations.bundleLib.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes(
            'Built-By'       : System.properties['user.name'],
            'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
            'Build-Revision' : versioning.info.commit,
            'Created-By'     : "Gradle ${gradle.gradleVersion}",
            'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
            'Version'        : "${gradle.rootProject.version}"
        )
    }
}
